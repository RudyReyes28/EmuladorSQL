package com.rudyreyes.emuladorsql;
import java.util.ArrayList;
import java_cup.runtime.*;
import com.rudyreyes.emuladorsql.modelo.*;

class ParserSQL;

parser code {:
    String asterisco = "";
    ArrayList<String> columnas = new ArrayList<>();
    ArrayList<Columna> actualizarColumnas = new ArrayList<>();
    ArrayList<Columna> condiciones = new ArrayList<>();
    ArrayList<String> operadoresLogicos = new ArrayList<>();
    ArrayList<String> valoresInsertar = new ArrayList<>();
    ArrayList<Object> consultas = new ArrayList<>();
    
    void limpiarEstructuras() {
        columnas.clear();
        condiciones.clear();
        operadoresLogicos.clear();
        valoresInsertar.clear();
        consultas.clear();
        actualizarColumnas.clear();
        asterisco = "";
        
    }

    ArrayList<Object> obtenerConsultas(){
        return consultas;
    }
:}


terminal SELECCIONAR, FILTRAR, INSERTAR, ACTUALIZAR, ASIGNAR, ELIMINAR, P_COMA, EN, PARENTESIS_ABRE;
terminal PARENTESIS_CIERRA, COMA, VALORES, ASTERISCO, SIMB_IGUAL, SIMB_MENOR, SIMB_MAYOR,SIMB_MENOR_IGUAL, SIMB_MAYOR_IGUAL ;
terminal SIMB_NO_IGUAL, OP_AND, OP_OR;
terminal String NOMBRECOLUMNA, LITERAL, NUMBER;

non terminal listas_instrucciones, instruccion, listado, tabla,valores, condiciones, condicion;
non terminal asignaciones, asignacion, columnas, operacion_relacional, operador_logico, empty;

non terminal String operador_relacional, literal;
non terminal InstruccionSeleccionar seleccionar;
non terminal InstruccionInsertar insertar;
non terminal InstruccionActualizar actualizar;
non terminal InstruccionEliminar eliminar;

start with listas_instrucciones;

listas_instrucciones ::= instruccion:i listas_instrucciones {: consultas.add(i); :}
                    | instruccion:i {: consultas.add(i); :}
                    ;

instruccion ::= seleccionar:s {:RESULT = s; :}
             | insertar:i {:RESULT = i; :}
             | actualizar: a {:RESULT = a; :}
             | eliminar:e {: RESULT = e; :}
             ;

seleccionar ::= SELECCIONAR listado EN LITERAL:p condiciones P_COMA {: 
    ArrayList<String> columnasCopia = new ArrayList<>(columnas);
    ArrayList<Columna> condicionesCopia = new ArrayList<>(condiciones);
    ArrayList<String> operadoresLogicosCopia = new ArrayList<>(operadoresLogicos);

    RESULT = new InstruccionSeleccionar(asterisco, columnasCopia, p, condicionesCopia, operadoresLogicosCopia);

    limpiarEstructuras();
   
    :}
            ;

listado ::= ASTERISCO {: asterisco = "*"; :}
	| columnas
        ;

insertar ::= INSERTAR EN LITERAL:p tabla VALORES PARENTESIS_ABRE valores PARENTESIS_CIERRA P_COMA
        {: 
        ArrayList<String> columnasCopia = new ArrayList<>(columnas);
         ArrayList<String> valoresCopia = new ArrayList<>(valoresInsertar);
         
        RESULT = new InstruccionInsertar(p, columnasCopia, valoresCopia);

        limpiarEstructuras();
        :}
        ;

tabla ::= PARENTESIS_ABRE columnas PARENTESIS_CIERRA
	| empty
        ;

valores ::= literal:li COMA valores {:valoresInsertar.add(li);:}
	| literal:li2 {:valoresInsertar.add(li2);:}
        ;

actualizar ::= ACTUALIZAR EN LITERAL:p ASIGNAR asignaciones condiciones P_COMA
                {:
                    ArrayList<Columna> columnasCopia = new ArrayList<>(actualizarColumnas);
                    ArrayList<Columna> condicionesCopia = new ArrayList<>(condiciones);
                    ArrayList<String> operadoresLogicosCopia = new ArrayList<>(operadoresLogicos);

                    RESULT = new InstruccionActualizar(p, columnasCopia, condicionesCopia, operadoresLogicosCopia);

                    limpiarEstructuras();
                :}
             ;

asignaciones ::= asignacion COMA asignaciones
              | asignacion
              ;

asignacion ::= NOMBRECOLUMNA:c SIMB_IGUAL literal:li
            {:
                actualizarColumnas.add(new Columna(c, "=" , li)); 
            :}
            ;

eliminar ::= ELIMINAR EN LITERAL:p condiciones P_COMA
                {:
                    ArrayList<Columna> condicionesCopia = new ArrayList<>(condiciones);
                    ArrayList<String> operadoresLogicosCopia = new ArrayList<>(operadoresLogicos);

                    RESULT = new InstruccionEliminar(p, condicionesCopia, operadoresLogicosCopia);

                    limpiarEstructuras();
                :}
            ;

columnas ::= NOMBRECOLUMNA:c1 COMA columnas {:columnas.add(c1);:}
	|NOMBRECOLUMNA:c2 {:columnas.add(c2);:}
        ;

condiciones ::= FILTRAR condicion
		| empty {: System.out.println("no se encontro nada"); :}
                ;

condicion ::= operacion_relacional operador_logico condicion
		| operacion_relacional
                ;

           

operacion_relacional ::= NOMBRECOLUMNA:c operador_relacional:o literal:li {:
    
    
    condiciones.add(new Columna(c, o , li)); 
:}
                    ;

literal ::= LITERAL:l {:RESULT = l; :}
            | NUMBER:n {:RESULT = n; :}
            ;

operador_relacional ::= SIMB_IGUAL:igual {:RESULT = "=";:}
                   | SIMB_MENOR:menor {:RESULT = "<";:}
                   | SIMB_MAYOR:mayor {:RESULT = ">";:}
                   | SIMB_MENOR_IGUAL:meigual {:RESULT = "<=";:}
                   | SIMB_MAYOR_IGUAL:maigual {:RESULT = ">=";:}
                   | SIMB_NO_IGUAL:noigual {:RESULT = "<>";:}
                   ;

operador_logico ::= OP_AND {: operadoresLogicos.add("AND"); :}
                | OP_OR {: operadoresLogicos.add("OR"); :}
                ;

empty ::= /* empty */ {: System.out.println("no se encontro nada"); :} ;